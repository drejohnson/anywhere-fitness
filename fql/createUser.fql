Update(Function("create_user"), {
  "body": Query(Lambda(["input"],
    Let(
      {
        userExist: Exists(
          Match(
            Index("user_by_email"), Casefold(Select("email", Var("input")))
          )
        ), 
      },
      If(
        Var("userExist"),
        { error: { status: "UserExists", message: "Email is already taken" } },
        Do(
          Create(Collection("User"), {
            data: {
              firstName: Select("firstName", Var("input")),
              lastName: Select("lastName", Var("input")),
              email: Select("email", Var("input")),
              role: Select("role", Var("input")),
            },
            credentials: {
              password: Select("password", Var("input"))
            }
          }),
          Let(
            {
              response: Login(
                Match(Index("user_by_email"), Select("email", Var("input"))),
                { password: Select("password", Var("input")) },
              )
            },
            {
              token: Select("secret", Var("response")),
              user: Select("instance", Var("response"))
            }
          )
        )
      )
    )
  ))
});